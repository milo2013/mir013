//===== 3CeAM Script =========================================
//= 3CeAM Custom Stylist NPC
//===== Original Script By: =====================================
//=  Rytech
//===== rAthena Ported By: =====================================
//=  Incik Rakuzas 
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= 3CeAM r791+
//=  rAthena 
//===== Description: =========================================
//= A new generation stylist that offers enhanced style
//= options and dressing room for character dye/style preview.
//= Stylist also detects the server's max dyes/styles settings
//= and also auto adjusts maxes between jobs as needed.
//===== Additional Comments: =================================
//= 1.0 First release. [Rytech]
//= 1.1 Modified script for enable using and support for rAthena emulator. [Incik Rakuzas]
//============================================================
milenia,65,150,11	script	Super Stylist	429,{
Lintro:
	mes "[Super Stylist]";
	mes "Hey there. Im a super stylist. Want to try out my services?";
	mes "I can customize your character's appearance in a hand full of ways.";
	next;
	menu "Sure.",Laccessroom,"No Thanks.",Lbye,"Super Stylist???",Lwhoareyou;

Laccessroom:
	// Open the dressing room and sets the dye/style max's depending on the job.
	opendressroom(0);
	set @hairstylemax,27;
	set @bodystylemax,1;
	set @hairdyemax,8;
	set @bodydyemax,3;

	// Summoner's only have 6 hair styles.
	if (Class != 4218) //Summoner
		set @hairstylemax,27;
	else
		set @hairstylemax,6;

	// Only 3rd job's have access to a 2nd body style.
	// Note: Not all of the new body styles are out. Best to limit only to jobs that do have them.
	if (
	//Class == 4051 || // Rune Knight Regular [x]
	//Class == 4060 || // Rune Knight Reborn [x]
	//Class == 4096 || // Rune Knight Baby [x]
	Class == 4055 || // Warlock Regular
	Class == 4061 || // Warlock Reborn
	Class == 4097 || // Warlock Baby
	Class == 4056 || // Ranger Regular
	Class == 4062 || // Ranger Reborn
	Class == 4098 || // Ranger Baby
	Class == 4057 || // Arch Bishop Regular
	Class == 4063 || // Arch Bishop Reborn
	Class == 4099 || // Arch Bishop Baby
	Class == 4058 || // Mechanic Regular
	Class == 4064 || // Mechanic Reborn
	Class == 4100 || // Mechanic Baby
	Class == 4059 || // GX Regular
	Class == 4065 || // GX Reborn
	Class == 4101 || // GX Baby
	Class == 4066 || // RG Regular
	Class == 4073 || // RG Reborn
	Class == 4102 || // RG Baby
	Class == 4078 || // Genetic Regular
	Class == 4071 || // Genetic Reborn
	Class == 4107 // Genetic Baby
	)
		set @bodystylemax,1;
	else
		set @bodystylemax,0;

Lmain:
	mes "[Super Stylist]";
	mes "Ok step this way. A dressing room window should open. Use this to decide what you want and then select in the menu what your interested in.";
	next;
	menu "All Colors And Styles",Lalldyestyle,"Hair Color Only",Lhairdye,"Hair Style Only",Lhairstyle,"Cloth Color Only",Lbodydye,"Body Style Only",Lbodystyle,"Im done.",Lbye;

Lalldyestyle:
	mes "[Super Stylist]";
	mes "Enter the Color/Styles you want in the 4 input boxes in order....";
	mes "Max Hair Color = "+@hairdyemax+"";
	mes "Max Hair Styles = "+@hairstylemax+"";
	mes "Max Body Color = "+@bodydyemax+"";
	mes "Max Body Styles = "+@bodystylemax+"";
	input @hairdye;
	if (@hairdye < 0 || @hairdye > @hairdyemax) set @hairdye,0;
	input @hairstyle;
	if (@hairstyle < 0 || @hairstyle > @hairstylemax) set @hairstyle,0;
	input @bodydye;
	if (@bodydye < 0 || @bodydye > @bodydyemax) set @bodydye,0;
	input @bodystyle;
	if (@bodystyle < 0 || @bodystyle > @bodystylemax) set @bodystyle,0;
	setlook 6,@hairdye;
	setlook 1,@hairstyle;
	setlook 7,@bodydye;
	setlook 13,@bodystyle;
	next;
	goto Lmain;

Lhairdye:
	mes "[Super Stylist]";
	mes "Enter the dye you want.";
	mes "Max Hair Color = "+@hairdyemax+"";
	input @hairdye;
	if (@hairdye < 0 || @hairdye > @hairdyemax) set @hairdye,0;
	setlook 6,@hairdye;
	next;
	goto Lmain;

Lhairstyle:
	mes "[Super Stylist]";
	mes "Enter the style you want.";
	mes "Max Hair Styles = "+@hairstylemax+"";
	input @hairstyle;
	if (@hairstyle < 0 || @hairstyle > @hairstylemax) set @hairstyle,0;
	setlook 1,@hairstyle;
	next;
	goto Lmain;

Lbodydye:
	mes "[Super Stylist]";
	mes "Enter the dye you want.";
	mes "Max Cloth Color = "+@bodydyemax+"";
	input @bodydye;
	if (@bodydye < 0 || @bodydye > @bodydyemax) set @bodydye,0;
	setlook 7,@bodydye;
	next;
	goto Lmain;

Lbodystyle:
	mes "[Super Stylist]";
	mes "Enter the style you want.";
	mes "Max Body Styles = "+@bodystylemax+"";
	input @bodystyle;
	if (@bodystyle < 0 || @bodystyle > @bodystylemax) set @bodystyle,0;
	setlook 13,@bodystyle;
	next;
	goto Lmain;

Lbye:
	mes "[Super Stylist]";
	mes "Ok then. Ill be here whenever you need to change your look.";
	close;

Lwhoareyou:
	mes "[Super Stylist]";
	mes "Never herd of us?";
	mes "We super stylist are a new generation of stylist that are able to provide character appearance customization to players.";
	next;
	mes "But the difference between us and regular stylist is that we have a dressing room that allows you to try out different styles and dyes freely all together.";
	mes "This allows you to get a full preview of what your char will look like if you applied all the styles and dyes selected before finalizing what you want.";
	mes "Better then doing one at a time.";
	next;
	mes "Super stylist can also change the appearance of the outfit you wear. We call them body styles. However, only 3rd job characters are offered this service at the moment.";
	mes "Also we don't have outfits ready for all of these jobs yet since this service is still very new and designs are still being worked on.";
	mes "Be sure to check back later if a style isnt available for your job as were releasing them one by one.";
	next;
	goto Lintro;
}