//====================================================================================
//Script Name: Vote For Points NPC Script for FluxCP
//SVN: Tested in rAthena r156513
//Developed By: JayPee Mateo
//Version: 1.0
//Requirement(s): FluxCP V4P Addon
//Description: This is a npc script for FluxCP Vote for points in order for the players
//to claim their vote points
//====================================================================================

milenia,130,159,11	script	Milenia Vote Shop	758,{

//Function Prototypes
function garbagecol;//Garbage collection for the Character variables
garbagecol();
function add_item; //Syntanx: add_item(ITEMID,QUANTITY,POINTS,CATEGORY);
function makeCategory;//This will return a list of the categories
function getItemsByCat;//This will return the list of items associated to the particular category
function getItemDetails;//This will return the details of the item
function getPoints;//This will return the points of the player stored in the database
function updatePoints;//This will updates the points of the player stored in the database


//NPC Name
set .npcname$,"[Points Exchanger]";
	
//Initialization of the Rewards
add_item(5372,1,60,"Cash Headgear");//Koneko Hat
add_item(18600,1,150,"Cash Headgear");//Cat Ear Beret
add_item(5457,1,80,"Cash Headgear");//Moon Rabbit hat
add_item(5495,1,40,"Cash Headgear");//Power of Thor 
add_item(5361,1,30,"Cash Headgear");//Gangster
add_item(5538,1,60,"Cash Headgear");//Tucan
add_item(5597,1,30,"Cash Headgear");//Chewing Gum
add_item(5596,1,30,"Cash Headgear");//4 Leaf Clover
add_item(5775,1,20,"Cash Headgear");//Donut In Mouth
add_item(5463,1,70,"Cash Headgear");//Rainbow Scarf
add_item(5767,1,70,"Cash Headgear");//Samurai Mask
add_item(5462,1,50,"Cash Headgear");//Spiked Scarf
add_item(5393,1,70,"Cash Headgear");//Cupid Target
add_item(5664,1,40,"Cash Headgear");//Filir Pinions
add_item(5516,1,60,"Cash Headgear");//Hawk Eyes
add_item(5840,1,40,"Cash Headgear");//Woe Elven Ears
add_item(5755,1,50,"Cash Headgear");//Silent executor
add_item(5748,1,70,"Cash Headgear");//Sniper Google
add_item(5579,1,50,"Cash Headgear");//Wanderer Sakkat
add_item(5483,1,40,"Cash Headgear");//Odin Mask
add_item(5293,1,60,"Cash Headgear");//Ramen Hat
add_item(5401,1,20,"Cash Headgear");//Black Frame Glasses
add_item(5325,1,80,"Cash Headgear");//robo eye
add_item(5362,1,60,"Cash Headgear");//ninja Scroll
add_item(18505,1,20,"Cash Headgear");//Umbala Spirit
add_item(5305,1,40,"Cash Headgear");//Pirate Dagger
add_item(5377,1,80,"Cash Headgear");//Gentleman's Pipe
add_item(5288,1,20,"Cash Headgear");//Red Glasses
add_item(5013,1,120,"Cash Headgear");//Lord Kaho's Horn
add_item(5291,1,90,"Cash Headgear");//Kettle Hat
add_item(5373,1,100,"Cash Headgear");//Dark Randgris Helm
add_item(5312,1,130,"Cash Headgear");//Ayotha king's Hat
add_item(5379,1,140,"Cash Headgear");//Tam
add_item(5383,1,100,"Cash Headgear");//Hunter's Cap 
add_item(18539,1,150,"Cash Headgear");///Skull's Cap
add_item(5276,1,60,"Cash Headgear");//Fantastic Wig
add_item(5421,1,70,"Cash Headgear");//Ear of Ifrit
add_item(5155,1,120,"Cash Headgear");//Father White Moustache
add_item(5548,1,40,"Cash Headgear");//Scarlet Rose
add_item(5470,1,60,"Cash Headgear");//Eye of Darkness
add_item(2204,1,130,"Cash Headgear");//Glasses [1]
add_item(5410,1,20,"Cash Headgear"); //Brown papper hat
add_item(5449,1,20,"Cash Headgear"); //Soloplay box
add_item(5315,1,20,"Cash Headgear"); //Observer
add_item(5565,1,90,"Cash Headgear"); //Dragon Arhat mask
add_item(5757,1,100,"Cash Headgear"); //Reissue Schmitz Helm
add_item(5378,1,60,"Cash Headgear"); //Bunny Top hat 
add_item(5503,1,60,"Cash Headgear"); //Magic Rabbit hat 
add_item(5529,1,90,"Cash Headgear"); //Santanic Bone Helm

//Script Start	
	mes .npcname$;
	mes "Hi! Do you want to exchange your vote points?:";
	switch(select("Yes, I want to exchange my points:See my points"))
	{
		case 1:
		next;
			mes .npcname$;
			mes "Please choose a category:";
			set .@selected,select(makeCategory())-1;
		next;
			mes .npcname$;
			mes "Please the item you want:";
			set .@selected,select(getItemsByCat(@listCat$[.@selected]))-1;
			
		next;
			mes .npcname$;						
			set .@ritemid,getItemDetails(@itemKeys[.@selected],"itemid");
			set .@rquantity,getItemDetails(@itemKeys[.@selected],"quantity");
			set .@rpoints,getItemDetails(@itemKeys[.@selected],"points");
			mes "Item ID:"+.@ritemid;
			mes "Item Name: "+getitemname(.@ritemid);
			mes "Item Quantity: "+.@rquantity+" pc(s).";
			mes "Required Points: "+.@rpoints+" pt(s).";
			mes "\n";
			mes "Do you want to this item?";
			if(select("Yes:No")==1)
			{
				set .@points,getPoints(getcharid(3));
				if(.@points>=.@rpoints)
				{
					next;
					mes .npcname$;			
					updatePoints(getcharid(3),.@rpoints);
					getitem .@ritemid,.@rquantity;
					mes "Here you go!. Thank you for voting. Don't forget to vote again. :D";
				}
				else
					mes "Sorry, you do not have enough points for this item.";				
			}
			else
			{
				next;
				mes .npcname$;			
				mes "Okay bye!";
			}			
			garbagecol();
		close;
		case 2:
			next;
			mes .npcname$;			
			set .@points,getPoints(getcharid(3));			
			mes "You currently have "+.@points+" pt(s).";
			garbagecol();
		close;
	}
end;

//Functions Bodies
	function updatePoints {
		set .@account_id,getarg(0);
		set .@usedPoints,getarg(1);
		query_sql("UPDATE `cp_v4p_voters` SET points=(points-"+.@usedPoints+") WHERE account_id='"+.@account_id+"'");
		return;
	}

	function getPoints {
		set .@account_id,getarg(0);
		query_sql("SELECT `points` FROM `cp_v4p_voters` WHERE account_id="+.@account_id+" LIMIT 1",.@points);
		if(getarraysize(.@points)==0)
			return 0;		
		return .@points[0];
	}

	function getItemDetails {
		set .@key,getarg(0); //Key
		set .@detail$,getarg(1); //What details to return such as ItemID, Points, Quantity, Category
		
		if(strtolower(.@detail$) == strtolower("ItemID"))
			return @itemID[.@key];
		else if(strtolower(.@detail$) == strtolower("Quantity"))
			return @itemQ[.@key];
		else if(strtolower(.@detail$) == strtolower("Points"))
			return @points[.@key];
		else if(strtolower(.@detail$) == strtolower("Category"))
			return @category$[.@key];
	}

	function getItemsByCat {		
		set .@selectedCat$,getarg(0);
		set .@make_string$,"";
		set .@x,0;
		for(set .@i,0; .@i<getarraysize(@category$); set .@i,.@i+1)
		{
			if(strtolower(.@selectedCat$) == strtolower(@category$[.@i]))
			{
				setarray @itemKeys[.@x],.@i;
				if(.@make_string$ == "")
					set .@make_string$,getitemname(@itemID[.@i]);
				else
					set .@make_string$,.@make_string$+":"+getitemname(@itemID[.@i]);
				
				set .@x,.@x+1;
			}
		}
		return .@make_string$;
	}
	
	
	function makeCategory {
		set .@make_string$,"";
		for(set .@i,0; .@i<getarraysize(@category$); set .@i,.@i+1)
		{			
			if(.@make_string$ == "")
			{
				setarray @listCat$[getarraysize(@listCat$)],@category$[.@i];
				set .@make_string$,@category$[.@i];
			}
			else
			{
				if(compare(.@make_string$,@category$[.@i])==0)
				{
					setarray @listCat$[getarraysize(@listCat$)],@category$[.@i];
					set .@make_string$,.@make_string$+":"+@category$[.@i];				
				}
			}
		}
		return .@make_string$;
	}
			
	function add_item
	{
		set .@itemID,getarg(0,-1); //IteID
		set .@itemQ,getarg(1,-1); //Item Quantity
		set .@points,getarg(2,-1);
		set .@cat$,getarg(3,"Uncategorized"); //Category
		
		
		if(.@itemID == -1)
		{
			debugmes "Invalid Item ID. Script not completely loaded.";
			end;
		}
		else if(.@itemQ == -1)
		{
			debugmes "Invalid Item Quantity. Script not completely loaded.";
			end;
		}
		else if(.@points == -1)
		{
			debugmes "Points assignment error. Script not completely loaded.";
			end;
		}
		set .@key,getarraysize(@itemID);
		setarray @itemID[.@key],.@itemID;
		setarray @itemQ[.@key],.@itemQ;
		setarray @points[.@key],.@points;
		setarray @category$[.@key],.@cat$;
		return 1; //return 1 as success
	}
	
	function garbagecol{
		deletearray @itemID[0],128;
		deletearray @itemQ[0],128;
		deletearray @points[0],128;	
		deletearray @category$[0],128;	
		deletearray @listCat$[0],128;
		deletearray @itemKeys[0],128;
		return;
	}
}